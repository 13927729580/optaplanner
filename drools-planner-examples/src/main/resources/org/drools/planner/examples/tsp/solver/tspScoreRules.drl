/*
 * Copyright 2011 JBoss Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.drools.planner.examples.tsp.solver;
    dialect "java"

import org.drools.planner.core.score.buildin.simple.SimpleScoreCalculator;
import org.drools.planner.core.score.constraint.IntConstraintOccurrence;
import org.drools.planner.core.score.constraint.ConstraintType;

import org.drools.planner.examples.tsp.domain.City;
import org.drools.planner.examples.tsp.domain.Depot;
import org.drools.planner.examples.tsp.domain.Journey;
import org.drools.planner.examples.tsp.domain.TravelingSalesmanTour;

global SimpleScoreCalculator scoreCalculator;

// ############################################################################
// Soft constraints
// ############################################################################

rule "distanceToPreviousTerminal"
    when
        $journey : Journey($distanceToPreviousTerminal : distanceToPreviousTerminal)
    then
        insertLogical(new IntConstraintOccurrence("distanceToPreviousTerminal", ConstraintType.NEGATIVE_SOFT,
                $distanceToPreviousTerminal,
                $journey));
end

rule "distanceFromLastJourneyToDepot"
    when
        $journey : Journey()
        not Journey (previousTerminal == $journey)
        $depot : Depot()
    then
        insertLogical(new IntConstraintOccurrence("distanceFromLastJourneyToDepot", ConstraintType.NEGATIVE_SOFT,
                $journey.getDistanceTo($depot),
                $journey));
end

// ############################################################################
// Calculate score
// ############################################################################

rule "softConstraintsBroken"
    when
        $softTotal : Number() from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.NEGATIVE_SOFT, $weight : weight),
            sum($weight) // Vote for http://jira.jboss.com/jira/browse/JBRULES-1075
        )
    then
        scoreCalculator.setScore(- $softTotal.intValue());
end
